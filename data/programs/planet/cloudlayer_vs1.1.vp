#include "gas_giants_params.h"
#include "../config.h"
#include "../stdlib_vp.h"
#include "../stdlib.h"

#define outCloudCoord gl_TexCoord[0].xy
#define outGroundCoord gl_TexCoord[0].zw
#define outShadowCoord gl_TexCoord[1].xy
#define outNoiseCoord gl_TexCoord[1].zw
#define outCityCoord gl_TexCoord[2].xy
#define outExtraShadowCoord2 gl_TexCoord[3].xy
#define outExtraShadowCoord3 gl_TexCoord[3].zw

varying vec3 varTSLight;
varying vec3 varTSView;
varying vec3 varWSNormal;

uniform float fTime;

float soffs(float lim, float x)
{
  return x + ((x>=0.0)?lim:-lim);
}

vec2 shadowExtrude(in vec2 texcoord, in vec3 direction, float mult)
{
  return texcoord + direction.xy * fAtmosphereExtrusion.x * fShadowRelHeight.x / soffs(0.01,direction.z) * mult;
}

void main()
{
   // Compute position, eye-to-object direction and normalized world-space normal
   vec4 extrudedPosition = gl_Vertex + vec4(normalize(gl_Normal.xyz),0) * fAtmosphereExtrusion.x * fShadowRelHeight.x * fvCloudLayers.x;
   vec3 normal   = normalize(gl_NormalMatrix * gl_Normal);
   vec3 tangent  = normalize(gl_NormalMatrix * gl_MultiTexCoord2.xyz);
   vec3 binormal = cross(tangent, normal) * sign(gl_MultiTexCoord2.w);
   
   // Extrude sphere
   vec4 position = gl_ModelViewMatrix * extrudedPosition;
   vec3 eyetopos = normalize(position.xyz);
   
   vec4 lightpos;
   float lightatt;
   lightPosSizeAndAtt0(position, lightpos, lightatt);
   
   // TS view vector
   varTSView.x = dot( tangent, -eyetopos );
   varTSView.y = dot( binormal, -eyetopos );
   varTSView.z = dot( normal, -eyetopos );
   
   // TS light posision
   varTSLight.x = dot( tangent, lightpos.xyz );
   varTSLight.y = dot( binormal, lightpos.xyz );
   varTSLight.z = dot( normal, lightpos.xyz );
   
   // WS normal
   varWSNormal = normal;
   
   // Load varyings
   float layerThickness = fvCloudLayers.x + 1.0 / fvCloudLayerScales.x - fvCloudLayers.w;
   outCloudCoord       = gl_MultiTexCoord0.xy + fTime*fvCloudLayerDrift.xy;
   outNoiseCoord       = gl_MultiTexCoord0.xy - fTime*fvCloudLayerDrift.xy;
   outCityCoord        = gl_MultiTexCoord0.xy;
   outGroundCoord      = shadowExtrude(outCloudCoord.xy, varTSView, layerThickness).xy;
   outShadowCoord      = shadowExtrude(outGroundCoord.xy, varTSLight, -1.0).xy;
   
   vec2 extraCloudCoord;
   vec2 extraGroundCoord;
   
   if (fvExtraCloudLayers.x < 1.0) {
        extraCloudCoord = gl_MultiTexCoord0.xy + fTime*fvCloudLayerDrift_Layer2.xy;
        extraGroundCoord = shadowExtrude(extraCloudCoord.xy, varTSView, layerThickness).xy;
        outExtraShadowCoord2 = lerp(extraGroundCoord, shadowExtrude(extraGroundCoord.xy, varTSLight, -fShadowRelHeight.x).xy, fCloudLayerThickness);
   } else {
        outExtraShadowCoord2 = outShadowCoord.xy;
   }
   
   if (fvExtraCloudLayers.y < 1.0) {
        extraCloudCoord = gl_MultiTexCoord0.xy + fTime*fvCloudLayerDrift_Layer3.xy;
        extraGroundCoord = shadowExtrude(extraCloudCoord.xy, varTSView, layerThickness).xy;
        outExtraShadowCoord3 = lerp(extraGroundCoord, shadowExtrude(extraGroundCoord.xy, varTSLight, -2.0*fShadowRelHeight.x).xy, fCloudLayerThickness);
   } else {
        outExtraShadowCoord3 = outShadowCoord.xy;
   }
   
   // set primary color to the emissive material properties
   vec4 pc = gl_FrontMaterial.emission;
   vec4 sc = vec4(0.0);
   pc.a = fvCloudColor.a * fCloudLayerDensity;
   
   pc.rgb += fvCloudColor.rgb * gl_LightSource[0].diffuse.rgb * lightatt;
   sc.rgb += fvCloudColor.rgb * gl_LightSource[0].specular.rgb * lightatt;
   
   // Need this instead of ftransform() for invariance
   gl_Position = gl_ModelViewProjectionMatrix * extrudedPosition;
   gl_FrontColor = gl_BackColor = pc;
   gl_FrontSecondaryColor = gl_BackSecondaryColor = sc;
}



