#include "earth_params.h"
#include "../stdlib_vp.h"
#include "../stdlib.h"

uniform float fTime;

#define varTSView gl_TexCoord[3]
#define varTSLight gl_TexCoord[4]
#define varScatter gl_TexCoord[5].rgb


float soffs(float lim, float x)
{
  return x + ((x>=0.0)?lim:-lim);
}

vec2 shadowExtrude(in vec2 texcoord, in vec3 direction)
{
  return texcoord - direction.xy * fAtmosphereExtrusion.x * fvCloudLayerDrift_ShadowRelHeight.w / soffs(0.2,direction.z);
}

float fresnel(float fNDotV)
{
   return fresnel(fNDotV,fFresnelEffect.x);
}


float  atmosphereLighting(float fNDotL) { return saturatef(min(1.0,2.0*fAtmosphereContrast*fNDotL)); }
float  groundLighting(float fNDotL) { return saturatef(min(1.0,2.0*fGroundContrast*fNDotL)); }

void main() 
{
  // Compute position, eye-to-object direction and normalized world-space normal
  vec4 position = gl_ModelViewMatrix * gl_Vertex;
  vec3 eyetopos = normalize(position.xyz);
  vec3 normal   = normalize(gl_NormalMatrix * gl_Normal);
  vec3 tangent  = normalize(gl_NormalMatrix * gl_MultiTexCoord2.xyz);
  vec3 binormal = cross(tangent, normal) * sign(gl_MultiTexCoord2.w);
  vec4 lightpos;
  float lightatt; 
  lightPosSizeAndAtt0(position, lightpos, lightatt);
  
  // Load varyings
  gl_TexCoord[0] = gl_MultiTexCoord0;
  
  // TS view vector
  varTSView.x = dot( tangent, -eyetopos );
  varTSView.y = dot( binormal, -eyetopos );
  varTSView.z = dot( normal, -eyetopos );
  
  // TS light posision
  varTSLight.x = dot( tangent, lightpos.xyz );
  varTSLight.y = dot( binormal, lightpos.xyz );
  varTSLight.z = dot( normal, lightpos.xyz );
  
  // Cloud & shadow coords
  vec2 cloudcoord = gl_MultiTexCoord0.xy + fTime*fvCloudLayerDrift_ShadowRelHeight.xy;
  vec2 clouddetail= gl_MultiTexCoord0.xy - fTime*fvCloudLayerDrift_ShadowRelHeight.xy;
  gl_TexCoord[1].zw = cloudcoord;
  gl_TexCoord[1].xy = shadowExtrude(cloudcoord, varTSLight.xyz);
  gl_TexCoord[2].xy = shadowExtrude(clouddetail, varTSLight.xyz);
  gl_TexCoord[2].zw = vec2(0.0);
  

  float fNDotV = saturatef( varTSView.z );
  float fNDotL = saturatef( varTSLight.z );
  
  // set primary color to the emissive material properties
  vec4 pc = gl_FrontMaterial.emission;
  vec4 sc = vec4(0.0);
  
  pc.a    = gl_FrontMaterial.diffuse.a;
  pc.rgb += gl_FrontMaterial.diffuse.rgb * gl_LightSource[0].diffuse.rgb * lightatt;
  sc.rgb += gl_FrontMaterial.specular.rgb * gl_LightSource[0].specular.rgb * lightatt;
  
  // self shadowing and spec scale
  pc.rgb *= self_shadow(fNDotL);
  sc.rgb *= 4.0*self_shadow_smooth_ex(fNDotL);

  // fresnel
  sc.rgb *= fresnel(fNDotV);
  
  // atmospheric absorption
  vec3 absorption = lerp(sqr(fAtmosphereAbsorptionColor.rgb),vec3(1.0),saturatef(sqr(fNDotV*fNDotL*2.0)));
  pc.rgb *= absorption;
  sc.rgb *= absorption;
  
  // atmospheric scatter
  float scattermuch = sqr(sqr(saturatef(1.0-fNDotV)));
  varScatter = atmosphereLighting(fNDotL)
                *lerp(fMinScatterFactor, fMaxScatterFactor, scattermuch)
                *fAtmosphereScatterColor.rgb;
  
  // Need this instead of ftransform() for invariance
  gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
  gl_FrontColor = gl_BackColor = pc;
  gl_FrontSecondaryColor = gl_BackSecondaryColor = sc;
}

